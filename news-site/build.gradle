
plugins {
	id 'application'
	id "com.github.ben-manes.versions" version "0.39.0"
	id 'org.kordamp.gradle.jdeps' version '0.15.0'
	id 'net.ritzow.news.build'
	//id "com.eriwen.gradle.css" version "2.14.0"
	//id "com.eriwen.gradle.js" version "2.14.1"
}

repositories {
	mavenCentral()
}

dependencies {
	implementation project(':jetstart')
	implementation 'org.eclipse.jetty:jetty-server:11.0.7'
	implementation 'com.j2html:j2html:1.5.0'
	implementation 'org.commonmark:commonmark:0.18.0'
	implementation 'com.zaxxer:HikariCP:5.0.0'
	implementation 'org.hsqldb:sqltool:2.6.1'
	//implementation 'com.h2database:h2:2.0.202'

	runtimeOnly 'org.hsqldb:hsqldb:2.6.1'
}

compileJava {
	options.encoding 'UTF-8'
}

import java.nio.file.Files
import java.nio.file.Path

def properties = new Properties()

try(def file = Files.newBufferedReader(projectDir.toPath().resolve('project.properties'))) {
	properties.load(file)
}

processResources {
	from('src/main') {
		include 'xml/**', 'lang/**', 'content/**'
	}
}

def static isOutOfDate(Path inputPath, Path outputPath) {
	return Files.notExists(outputPath) ||
			Files.getLastModifiedTime(inputPath).toInstant()
				.isAfter(Files.getLastModifiedTime(outputPath).toInstant())
}

jar {
	outputs.upToDateWhen { false }

	def outputDir = sourceSets.main.output.resourcesDir.toPath()

	doFirst {
		def inputCss = project.projectDir.toPath().resolve('src/main/css/global.scss')
		def outputCss = outputDir.resolve('css/global.css')
		mkdir outputDir
		if(isOutOfDate(inputCss, outputCss)) {
			exec {
				executable properties.getProperty("sass.path")
				args '--no-source-map', inputCss, outputCss
				standardInput = System.in
				standardOutput = System.out
				errorOutput = System.err
			}.assertNormalExitValue()
		}

		def inputIcon = project.projectDir.toPath().resolve("src/main/image/icon.svg")
		def outputIcon = outputDir.resolve("image/icon.svg")

		if(isOutOfDate(inputIcon, outputIcon)) {
			exec { /* TODO use scour to optimize svg files */
				executable properties.getProperty("inkscape.path")
				args '--export-filename=' + outputIcon, '--export-type=svg', '--export-plain-svg', inputIcon.toString()
				standardOutput = System.out
				errorOutput = System.err
			}.assertNormalExitValue()
		}
	}
}

run {
	mainClass.set('net.ritzow.news.RunSite')
	mainModule.set('net.ritzow.news')

	standardInput = System.in

	jvmArgs '-Dnet.ritzow.certs=' + properties.getProperty('keystore.file'),
			'-Dnet.ritzow.pass=' + properties.getProperty('keystore.password'),
			'-Dnet.ritzow.debug=true'
}